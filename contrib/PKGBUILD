# Maintainer: Garrit Franke <garrit@slashdev.space>
# Contributor: Alexey Yerin <yyp@disroot.org>
pkgname="antimony-git"
pkgver=v0.5.1.r1.fd08c6a
pkgrel=1
pkgdesc="The bullshit-free programming language"
url="https://github.com/antimony-lang/antimony"
license=("Apache")
arch=("x86_64" "pentium4" "i686")
makedepends=("git" "cargo")
optdepends=(
	"node: Node.js backend"
	"llvm: LLVM backend"
)
provides=("antimony")
source=(
	"antimony::git+https://github.com/antimony-lang/antimony"
)
sha256sums=("SKIP")

pkgver() {
	cd "${srcdir}/antimony"

	printf "%s" "$(git describe --long | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

build() {
	cd "${srcdir}/antimony"

	cargo build --release
}

check() {
	cd "${srcdir}/antimony"

	cargo test
}

package() {
	cd "${srcdir}/antimony"

	install -Dm755 target/release/sb "${pkgdir}/usr/bin/sb"
}
